/*
 *     SmartCatalogSPL, an Android catalog Software Product Line
 *     Copyright (c) 2016 Gianei Leandro Sebastiany
 *
 *     This program is free software: you can redistribute it and/or modify
 *     it under the terms of the GNU General Public License as published by
 *     the Free Software Foundation, either version 3 of the License, or
 *     (at your option) any later version.
 *
 *     This program is distributed in the hope that it will be useful,
 *     but WITHOUT ANY WARRANTY; without even the implied warranty of
 *     MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 *     GNU General Public License for more details.
 *
 *     You should have received a copy of the GNU General Public License
 *     along with this program.  If not, see <http://www.gnu.org/licenses/>.
 */

// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.2.0'

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files

        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.8'
        classpath 'com.google.gms:google-services:3.0.0'
    }
}

allprojects {
    repositories {
        jcenter()
    }
}

ext {

    androidCompileSdkVersion = 23
    androidBuildToolsVersion = '24.0.2'

    appInstanceDitlantaConfig = {
        applicationId "com.glsebastiany.smartcatalogspl.instanceditlanta"
        minSdkVersion 19
        targetSdkVersion 23
        versionCode 1
        versionName "1.0.0"
    }

    appInstanceFoodConfig = {
        applicationId "com.glsebastiany.smartcatalogspl.instancefood"
        minSdkVersion 19
        targetSdkVersion 23
        versionCode 1
        versionName "1.0.0"
    }

    librariesDefaultConfig = {
        minSdkVersion 19
        targetSdkVersion 23
    }

    glide = {
        compile 'com.github.bumptech.glide:glide:3.7.0'
    }

    rxAndroidVersion = '1.2.1'
    rxJavaVersion = '1.1.7'

    rxAndroid = {
        compile "io.reactivex:rxandroid:${rxAndroidVersion}"
        compile "io.reactivex:rxjava:${rxJavaVersion}"
    }

    javaxInjectVersion = '1'
    javaxInject = {
        compile "javax.inject:javax.inject:${javaxInjectVersion}"
    }

    daggerVersion = '2.1'
    dagger = {
        apt "com.google.dagger:dagger-compiler:${daggerVersion}"
        compile "com.google.dagger:dagger:${daggerVersion}"
        provided "org.glassfish:javax.annotation:10.0-b28"
    }

    androidAnnotationsVersion = '4.0.0'
    androidAnnotations = {
        apt "org.androidannotations:androidannotations:${androidAnnotationsVersion}"
        compile "org.androidannotations:androidannotations-api:${androidAnnotationsVersion}"
    }

    androidSupportVersion = '24.1.0'
    androidSupport = {
        compile "com.android.support:recyclerview-v7:${androidSupportVersion}"
        compile "com.android.support:appcompat-v7:${androidSupportVersion}"
        compile "com.android.support:cardview-v7:${androidSupportVersion}"
        compile "com.android.support:palette-v7:${androidSupportVersion}"
        compile "com.android.support:design:${androidSupportVersion}"
    }

    greenDaoVersion = '2.2.0'
    orm = {
        compile "org.greenrobot:greendao:${greenDaoVersion}"
    }
    ormGenerator = {
        compile "org.greenrobot:greendao-generator:${greenDaoVersion}"
    }

    jUnit = {
        testCompile 'junit:junit:4.12'
    }

    firebaseVersion = '9.4.0'
    firebaseUiVersion = '0.4.4'
    firebase = {
        compile "com.google.android.gms:play-services-auth:${firebaseVersion}"
        compile "com.google.firebase:firebase-database:${firebaseVersion}"
        compile "com.google.firebase:firebase-auth:${firebaseVersion}"
        compile "com.firebaseui:firebase-ui:${firebaseUiVersion}"
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
